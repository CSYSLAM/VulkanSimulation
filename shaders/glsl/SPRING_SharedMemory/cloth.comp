#version 450

struct Particle {
	vec4 pos;
	vec4 vel;
	vec4 uv;
	vec4 normal;
};

layout(std430, binding = 0) buffer ParticleIn {
	Particle particleIn[ ];
};

layout(std430, binding = 1) buffer ParticleOut {
	Particle particleOut[ ];
};

layout (local_size_x = 10, local_size_y = 10) in;

layout (binding = 2) uniform UBO 
{
	float deltaT;
	float particleMass;
	float springStiffness;
	float damping;
	float restDistH;
	float restDistV;
	float restDistD;
	float sphereRadius;
	vec4 spherePos;
	vec4 gravity;
	ivec2 particleCount;
} params;

layout (push_constant) uniform PushConsts {
	uint calculateNormals;
} pushConsts;

shared vec4 sharedPositions[10][10];

vec3 springForce(vec3 p0, vec3 p1, float restDist) 
{
	vec3 dist = p0 - p1;
	return normalize(dist) * params.springStiffness * (length(dist) - restDist);
}

void main() 
{
	uvec3 id = gl_GlobalInvocationID; 

	uint index = id.y * params.particleCount.x + id.x;
	if (index > params.particleCount.x * params.particleCount.y) 
		return;

	// Load particle positions into shared memory
	uint localX = gl_LocalInvocationID.x;
	uint localY = gl_LocalInvocationID.y;
	sharedPositions[localY][localX] = particleIn[index].pos;

	// Synchronize to ensure all positions are loaded
	barrier();

	// Initial force from gravity
	vec3 force = params.gravity.xyz * params.particleMass;

	vec3 pos = sharedPositions[localY][localX].xyz;
	vec3 vel = particleIn[index].vel.xyz;

	// Spring forces from neighboring particles
	// 左侧
	force += springForce(mix(particleIn[index-1].pos.xyz, sharedPositions[localY][localX-1].xyz, float(localX > 0)), pos, params.restDistH) * float(id.x > 0);

	// 右侧
	force += springForce(mix(particleIn[index+1].pos.xyz, sharedPositions[localY][localX+1].xyz, float(localX < 9)), pos, params.restDistH) * float(id.x < params.particleCount.x - 1);

	// 上方
	force += springForce(mix(particleIn[index + params.particleCount.x].pos.xyz, sharedPositions[localY+1][localX].xyz, float(localY < 9)), pos, params.restDistV) * float(id.y < params.particleCount.y - 1);

	// 下方
	force += springForce(mix(particleIn[index - params.particleCount.x].pos.xyz, sharedPositions[localY-1][localX].xyz, float(localY > 0)), pos, params.restDistV) * float(id.y > 0);

	// 左上
	force += springForce(mix(particleIn[index + params.particleCount.x - 1].pos.xyz, sharedPositions[localY+1][localX-1].xyz, float(localX > 0 && localY < 9)), pos, params.restDistD) * float(id.x > 0 && id.y < params.particleCount.y - 1);

	// 左下
	force += springForce(mix(particleIn[index - params.particleCount.x - 1].pos.xyz, sharedPositions[localY-1][localX-1].xyz, float(localX > 0 && localY > 0)), pos, params.restDistD) * float(id.x > 0 && id.y > 0);

	// 右上
	force += springForce(mix(particleIn[index + params.particleCount.x + 1].pos.xyz, sharedPositions[localY+1][localX+1].xyz, float(localX < 9 && localY < 9)), pos, params.restDistD) * float(id.x < params.particleCount.x - 1 && id.y < params.particleCount.y - 1);

	// 右下
	force += springForce(mix(particleIn[index - params.particleCount.x + 1].pos.xyz, sharedPositions[localY-1][localX+1].xyz, float(localX < 9 && localY > 0)), pos, params.restDistD) * float(id.x < params.particleCount.x - 1 && id.y > 0);

	force += (-params.damping * vel);

	// Integrate
	vec3 f = force * (1.0 / params.particleMass);
	particleOut[index].pos = vec4(pos + vel * params.deltaT + 0.5 * f * params.deltaT * params.deltaT, 1.0);
	particleOut[index].vel = vec4(vel + f * params.deltaT, 0.0);

	// Sphere collision
	vec3 sphereDist = particleOut[index].pos.xyz - params.spherePos.xyz;
	if (length(sphereDist) < params.sphereRadius + 0.01) {
		// If the particle is inside the sphere, push it to the outer radius
		particleOut[index].pos.xyz = params.spherePos.xyz + normalize(sphereDist) * (params.sphereRadius + 0.01);		
		// Cancel out velocity
		particleOut[index].vel = vec4(0.0);
	}

	// Normals
	if (pushConsts.calculateNormals == 1) {
		vec3 normal = vec3(0.0);
		vec3 a, b, c;
		if (id.y > 0) {
			if (id.x > 0) {
				a = particleIn[index - 1].pos.xyz - pos;
				b = particleIn[index - params.particleCount.x - 1].pos.xyz - pos;
				c = particleIn[index - params.particleCount.x].pos.xyz - pos;
				normal += cross(a,b) + cross(b,c);
			}
			if (id.x < params.particleCount.x - 1) {
				a = particleIn[index - params.particleCount.x].pos.xyz - pos;
				b = particleIn[index - params.particleCount.x + 1].pos.xyz - pos;
				c = particleIn[index + 1].pos.xyz - pos;
				normal += cross(a,b) + cross(b,c);
			}
		}
		if (id.y < params.particleCount.y - 1) {
			if (id.x > 0) {
				a = particleIn[index + params.particleCount.x].pos.xyz - pos;
				b = particleIn[index + params.particleCount.x - 1].pos.xyz - pos;
				c = particleIn[index - 1].pos.xyz - pos;
				normal += cross(a,b) + cross(b,c);
			}
			if (id.x < params.particleCount.x - 1) {
				a = particleIn[index + 1].pos.xyz - pos;
				b = particleIn[index + params.particleCount.x + 1].pos.xyz - pos;
				c = particleIn[index + params.particleCount.x].pos.xyz - pos;
				normal += cross(a,b) + cross(b,c);
			}
		}
		particleOut[index].normal = vec4(normalize(normal), 0.0f);
	}
}